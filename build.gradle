plugins {
    id "idea"
    id "com.craigburke.client-dependencies" version "1.3.1"
    id "com.eriwen.gradle.js" version "2.14.1"
    id "com.eriwen.gradle.css" version "2.14.0"
    id "org.kordamp.gradle.livereload" version "0.2.1"
}

task copyHtml(type: Copy) {
    from "${projectDir}/index.html"
    into "${buildDir}"
}

clientDependencies {
    installDir = "${buildDir}/node_modules"
    cacheDir = "${buildDir}/client-cache/"
    useGlobalCache = true
    checkDownloads = true
    npm {
        't-shirt'('1.1.0') {
            include '**/*.js'
        }
    }
}

lesscss {
    source = "${projectDir}/src/less"
    dest = "${buildDir}/css"
}

combineCss {
    source = lesscss.dest
    dest = file("${buildDir}/app-combined.css")
}
combineCss.dependsOn lesscss

minifyCss {
    source = combineCss.dest
    dest = file("${buildDir}/app.css")
}
minifyCss.dependsOn combineCss

//"minifyJs" actually also bundles JS as well
minifyJs {
    source = ["${projectDir}/lib", "${projectDir}/src/javascript", clientDependencies.installDir]
    //source.each{ logger.warn ("$it") }
    dest = file("${buildDir}/app.js")
    sourceMap = file("${buildDir}/app.js.map")
    closure {
        warningLevel = 'QUIET'
        compilerOptions.languageIn="ECMASCRIPT6"
        compilerOptions.languageOut="ECMASCRIPT5"
        compilerOptions.processCommonJSModules = true
        compilerOptions.moduleRoots = [clientDependencies.installDir.toString()]
        compilationLevel = "ADVANCED_OPTIMIZATIONS"
    }
}
minifyJs.dependsOn clientInstall

task(together) << {
    println "Ready to go :)"
}
together.dependsOn copyHtml
together.dependsOn minifyJs
together.dependsOn minifyCss

liveReload {
    docRoot = file("${projectDir}/build")
}